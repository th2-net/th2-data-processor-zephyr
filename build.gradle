plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'com.palantir.docker' version '0.25.0'
    id 'application'
}

group 'com.exactpro.th2'
version release_version

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"

    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

ext {
    ktorVersion = '1.5.4'
}

dependencies {
    // BOM with external dependencies restrictions
    api platform('com.exactpro.th2:bom:3.0.0')

    // the library for interaction with infra API
    implementation 'com.exactpro.th2:common:3.16.3'
    implementation 'com.exactpro.th2:grpc-crawler-data-service:0.0.1-check2_template-982469797-SNAPSHOT'

    // JIRA
    implementation group: 'com.atlassian.jira', name: 'jira-rest-java-client-core', version: '5.2.2'
    implementation group: 'io.atlassian.fugue', name: 'fugue', version: '4.7.2'
    implementation('com.atlassian.jwt:jwt-core:3.2.0')
    implementation('com.atlassian.jwt:jwt-api:3.1.0')

    // Ktor
    implementation("io.ktor:ktor-client:$ktorVersion")
    implementation("io.ktor:ktor-client-java:$ktorVersion")
    implementation("io.ktor:ktor-client-auth:$ktorVersion")
    implementation("io.ktor:ktor-client-logging:$ktorVersion")
    implementation("io.ktor:ktor-client-jackson:$ktorVersion")

    // Logging. All versions are specified in BOM dependency
    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:slf4j-api"

    // Idiomatic logging for Kotlin. Wraps slf4j
    implementation 'io.github.microutils:kotlin-logging:2.0.4'

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

jar {
    manifest {
        attributes(
                'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title':      '',
                'Specification-Vendor':     'Exactpro Systems LLC',
                'Implementation-Title':     project.archivesBaseName,
                'Implementation-Vendor':    'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version':   project.version
        )
    }
}

application {
    mainClass.set('com.exactpro.th2.dataservice.zephyr.BoxMain')
}

applicationName = 'service'

distTar {
    archiveFileName.set("${applicationName}.tar")
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}